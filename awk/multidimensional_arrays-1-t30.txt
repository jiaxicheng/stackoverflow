# XiCheng Jia Summer 2020 @ New York  2020/07/10
# awk code to calculate standard deviation for several columns
# REF: https://stackoverflow.com/questions/62778563
# Use of Multi-dimensional arrays
# Run with the following command:
# $ awk -f t30.awk ifile*.txt
# $ cat t30.awk

{
  nMax = FNR > nMax ? FNR : nMax                        # get the max FNR from all files
  for (j=3; j<=NF; j++) {
    if ($j == "?") continue
    v[FNR, j] = v[FNR, j] == "" ? $j : v[FNR, j] FS $j  # concatenate values of (FNR,j) in `v` using FS
    t[FNR, j] += $j                                     # calculate total for each (FNR,j)
  }
}
END {
  for (i=1; i<=nMax; i++) {
    printf("%d\t", i)
    for (j=3; j<=NF; j++) {
      if ((i,j) in t) {         # if (i,j) exists, split v into vals using default FS
        n = split(v[i,j], vals)
        if (n == 1) {           # print "?" if only 1 item in array vals
          printf("?")
        } else {                # otherwise, calculate mean `e`, sum `s` and then std
          e = t[i,j]/n
          s = 0
          for(x in vals) s += (vals[x]-e)**2
          printf("%.2f", sqrt(s/(n-1)))
        }
      } else { # print "?" if (i,j) not exists
        printf("?")
      }
      printf(j==NF?"\n":"\t")
    }
  }
}


#Another similar question from Apr2022:
# REF https://stackoverflow.com/q/71960266/3043253
# cat script3_4.awk

BEGIN { FS="," }

{ medal_total += $9+$10+$11 }
$3 != country {next}

{
    height[$4] = height[$4] == "" ? $6 : height[$4] FS $6
    weight[$4] = weight[$4] == "" ? $7 : weight[$4] FS $7
    height_total[$4] += $6
    weight_total[$4] += $7
    medals[$4] += $9+$10+$11
}

END {
    printf("Country,Sex,Weight_avg,Weight_std,Height_avg,Height_std,% Medals\n")
    for (i in weight) {
      /* calculate mean and std for weight */
      n = split(weight[i], vals)
      e_w = weight_total[i]/n
      s_w = 0;
      for(x in vals) s_w += (vals[x]-e_w)**2;
      s_w = n==1 ? 0 : sqrt(s_w/(n-1))

      /* calculate mean and std for height */
      n = split(height[i], vals)
      e_h = height_total[i]/n
      s_h = 0;
      for(x in vals) s_h += (vals[x]-e_h)**2;
      s_h = n==1 ? 0 : sqrt(s_h/(n-1))

      /* calculate medal percent */
      m_p = medals[i]*100/medal_total

      /* output */
      printf("%s,%s,%.4f Kg,%.4f,%.4f m,%.4f,%.4f%%\n",country,i,e_w,s_w,e_h,s_h,m_p)
    }
}

